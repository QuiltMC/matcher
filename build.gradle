plugins {
	id 'java-library'
	id 'application'
	id 'maven-publish'
	id 'org.openjfx.javafxplugin' version '0.0.8'
	id 'de.jjohannes.extra-java-module-info' version '0.5'
}

repositories {
	mavenCentral()
	maven {
		name "Quilt"
		url 'https://maven.quiltmc.org/repository/release/'
	}
}

archivesBaseName = 'matcher'
group = 'org.quiltmc'

sourceSets {
	main {
		java
		resources
	}
}

javafx {
	version = "11.0.2"
	modules = [ 'javafx.controls', 'javafx.web' ]
}

java {
	modularity.inferModulePath = true
	sourceCompatibility = JavaVersion.VERSION_11
	targetCompatibility = JavaVersion.VERSION_11

	if (!JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_11)) {
		toolchain {
			languageVersion = JavaLanguageVersion.of(11)
		}
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 11
}

dependencies {
	implementation "com.github.javaparser:javaparser-core:3.8.3"
	implementation "org.quiltmc:procyon-quilt-compilertools:0.5.35.local"
	api "org.ow2.asm:asm:9.0"
	implementation "org.ow2.asm:asm-commons:9.0"
	api "org.ow2.asm:asm-tree:9.0"
	implementation "org.ow2.asm:asm-util:9.0"
	implementation "org.quiltmc:cfr:0.0.2"
	implementation "org.quiltmc:quiltflower:1.3.0"
}

// TODO: Add Automatic-Module-Name to all these in Quilt forks
extraJavaModuleInfo {
	automaticModule("cfr-0.0.2.jar", "cfr")
	automaticModule("procyon-quilt-compilertools-0.5.35.local.jar", "procyon.compilertools")
	automaticModule("procyon-quilt-core-0.5.35.local.jar", "procyon.core")
	automaticModule("quiltflower-1.3.0.jar", "intellij.fernflower")
}

application {
	mainModule = 'matcher'
	mainClass = 'matcher.Main'
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
}